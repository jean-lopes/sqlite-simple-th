ordering-term
  = expr, [ 'COLLATE', collation-name ], 'ASC' | 'DESC';

filter
  = 'filter', '(', 'where', expr, ')';

type-name
  = name, { name }, [ '(', [ signed-number, [ ',', signed-number ] ], ')' ];

signed-number
  = [ sign ], numeric-literal;


literal-value
  = numeric-literal
  | string-literal
  | blob-literal
  | 'null'
  | 'true'
  | 'false'
  | 'current_time'
  | 'current_date'
  | 'current_timestamp'
  ;

numeric-literal
  = natural-literal
  | float-literal
  | hex-literal
  ;

natural-literal
  = digit, { digit }, [ scientific-sufix ]

float-literal
  = [ digit, { digit } ], '.', digit, { digit }, [ scientific-sufix ];
  
scientific-sufix
  = 'e', [ sign ], digit, { digit };

hex-literal
  = '0x', hex-digit, { hex-digit };

string-literal
  = "'", { char - "'"  | "''" }, "'";  

blob-literal
  = "x'", { hex-digit }, "'";

char 
  = ? any ASCII character ?;

letter
  = ? any ASCII letter character ?;

sign
  = '+'
  | '-'
  ;
  
digit 
  = '0' | '1' | '2' | '3'
  | '4' | '5' | '6' | '7'
  | '8' | '9' ;

hex-digit
  = '0' | '1' | '2' | '3' 
  | '4' | '5' | '6' | '7'
  | '8' | '9' | 'a' | 'b'
  | 'c' | 'd' | 'e' | 'f'
  ;
